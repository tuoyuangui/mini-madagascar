import os, sys
import bldutil, rsf.doc

################################################################################

Import('env bindir pkgdir system')

bldutil.py_install('__init__', env, pkgdir)
bldutil.py_install('__init__', env, os.path.join(pkgdir,'user'))

dirs = Split('plot libplot plplot pens sumain suplot') + system 

env.Command('prog.py',['sf'+x+'.py' for x in dirs],
            action=Action(rsf.doc.getprogs))
env.Depends('prog.py',['vpplot.py','doc.py'])


env.Command('version.py','#/VERSION.txt',
            action=Action(rsf.doc.getversion))
env.Depends('version.py','doc.py')

for src in Split('path doc flow proj prog  suproj conf version node'):
    bldutil.py_install(src, env, pkgdir)

for src in Split('latex2wiki sftour scons2jupyter'):
    bldutil.py_install(src, env, pkgdir)
    env.Command(src,src+'.py','cp $SOURCE $TARGET')
    binary = env.Install(bindir,src)
    env.AddPostAction(binary,Chmod(binary,0o755))

env.Command('sfversion','version.py','cp $SOURCE $TARGET')
env.Install(bindir,'sfversion')

env.Command('sfdoc','sfdoc.py','echo "#!%s" > $TARGET && cat $SOURCE >> $TARGET' % sys.executable)
binary = env.Install(bindir,'sfdoc')
env.AddPostAction(binary,Chmod(binary,0o755))

SConscript(dirs='gui',name='SConscript',exports='env bindir pkgdir system')

##########################################################################
# Self-documentation

Doc = Builder (action = Action(rsf.doc.selfdoc,varlist=['rsfprefix','lang','version','strip']),
               src_suffix='.c',suffix='.py',source_scanner=None)
env.Append(BUILDERS = {'Doc' : Doc})
